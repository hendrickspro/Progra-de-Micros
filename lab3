; Registros
.def temp      = r16
.def unidades  = r17
.def decenas   = r18
.def ms_low    = r19
.def ms_high   = r20
.def mux       = r21

; Vectores
.cseg
.org 0x0000
rjmp start

.org 0x001C
rjmp TIMER0_COMPA_ISR

;====================================================
; TABLA 7 SEGMENTOS (CÁTODO COMÚN)
; A=PD0, B=PD1, C=PD2, D=PD3, E=PD4, F=PD5, G=PD6
;====================================================
.org 0x0040
table7seg:
.DB 0x3F, 0x06, 0x5B, 0x4F, 0x66, 0x6D, 0x7D, 0x07, 0x7F, 0x6F, 0x77
;     0     1     2     3     4     5     6     7     8     9     10

; START
start:
    clr r1

; Desactivar USART (Uso de PD0 y PD1)
    ldi temp, 0x00
    sts UCSR0B, temp

; Configurar PD0–PD6 como salida
    ldi temp, 0x7F
    out DDRD, temp

; Limpiar PORTD
    clr temp
    out PORTD, temp

; PB2 y PB3 como salida (multiplexado)
    ldi temp, (1<<PB2)|(1<<PB3)
    out DDRB, temp

; Inicializar variables
    clr unidades
    clr decenas
    clr ms_low
    clr ms_high
    clr mux

; TIMER0 CTC 1ms EXACTO

; Modo CTC
    ldi temp, (1<<WGM01)
    out TCCR0A, temp

; Prescaler 64
    ldi temp, (1<<CS01)|(1<<CS00)
    out TCCR0B, temp

; 16MHz /64 = 250kHz
; 250 cuentas = 1ms
    ldi temp, 249
    out OCR0A, temp

; Habilitar interrupción
    ldi temp, (1<<OCIE0A)
    sts TIMSK0, temp

    sei

main:
    rjmp main

; ISR TIMER0
TIMER0_COMPA_ISR:

; Apagar displays
    cbi PORTB, PB2
    cbi PORTB, PB3

; Multiplexado
    tst mux
    breq mostrar_unidades

mostrar_decenas:
    mov temp, decenas
    rcall get_pattern
    out PORTD, temp
    sbi PORTB, PB2
    clr mux
    rjmp continuar

mostrar_unidades:
    mov temp, unidades
    rcall get_pattern
    out PORTD, temp
    sbi PORTB, PB3
    ldi mux,1

continuar:

; Contador exacto 1000 ms
    inc ms_low
    cpi ms_low, 250
    brne fin_isr

    clr ms_low
    inc ms_high
    cpi ms_high, 4
    brne fin_isr

;----- 1 SEGUNDO EXACTO -----
    clr ms_high

    inc unidades
    cpi unidades,10
    brlo fin_isr

    clr unidades
    inc decenas
    cpi decenas,6
    brlo fin_isr

; Si llega a 60 → reset
    clr decenas

fin_isr:
    reti

; Funcion Tabla
get_pattern:
    ldi ZH, high(table7seg<<1)
    ldi ZL, low(table7seg<<1)
    add ZL, temp
    adc ZH, r1
    lpm temp, Z
    ret
